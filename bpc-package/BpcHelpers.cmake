macro( bpc_set_if_not_set variable value )
	if( NOT ${variable} )
		set( ${variable} "${value}" )
	endif()
endmacro()

macro( bpc_set_path_if_not_set_and_make_absolute path_variable path_value )
	bpc_set_if_not_set( ${path_variable} "${path_value}" )
	get_filename_component( ${path_variable} "${${path_variable}}" ABSOLUTE )
endmacro()

macro( bpc_if_not_defined_set variable value )
	if( NOT DEFINED ${variable} )
		set( ${variable} "${value}" )
	endif()
endmacro()

function( bpc_list_intersect return left right )
	set( intersection )
	foreach( item ${left} )
		list( FIND right ${item} index )
		if( NOT (index EQUAL -1) )
			list( APPEND intersection ${item} )
		endif()
	endforeach()
	set( ${return} ${intersection} PARENT_SCOPE )
endfunction()

function( bpc_list_unite return left right )
	set( union ${left} )
	foreach( item ${right} )
		list( FIND left ${item} index )
		if( index EQUAL -1 )
			list( APPEND union ${item} )
		endif()
	endforeach()
	set( ${return} ${union} PARENT_SCOPE )
endfunction()

function( bpc_list_subtract return left right )
	set( difference )
	foreach( item ${left} )
		list( FIND right ${item} index )
		if( index EQUAL -1 )
			list( APPEND difference ${item} )
		endif()
	endforeach()
	set( ${return} ${difference} PARENT_SCOPE )
endfunction()

function( bpc_list_subtract_patterns return left patterns )
	set( excludes )
	foreach( item ${left} )
		set( index -1 )
		foreach( pat ${patterns} )
			if( ${item} MATCHES ${pat} )
				list( APPEND excludes ${item} )
			endif()
		endforeach()
	endforeach()
	
	bpc_list_subtract( difference "${left}" "${excludes}" ) 
	set( ${return} ${difference} PARENT_SCOPE )
endfunction()

function( bpc_get_include_dirs return target )
	if( NOT TARGET ${target} )
		set( ${return} "" PARENT_SCOPE )
		return()
	endif()
	
	# My own
	get_target_property(incdirs ${target} INCLUDE_DIRECTORIES)
	if ( NOT incdirs )
		set( incdirs )
	endif()
		
	# Transitive from dependencies:
	get_property( bpc_incdirs TARGET ${target} PROPERTY BPC_INCLUDE_DIRECTORIES  )
	bpc_list_unite( incdirs "${incdirs}" "${bpc_incdirs}" )
	
	set( ${return} ${incdirs} PARENT_SCOPE )
endfunction()

message( STATUS "Removing excluded libraries ${LIB_EXCLUDES}" )
bpc_list_subtract( LIBRARIES "${LIBRARIES}" "${LIB_EXCLUDES}" )